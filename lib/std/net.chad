fn uv_err(int code) T!
  strbuf errMessage = strbuf(1024)
  include
    uv_err_name_r(*_code, _errMessage._base, 1024);
  T! err = Err(str(errMessage))

  return err

pub struct TcpSocket
  ptr[void] handle
  bool alreadyClosed

fn drop(mut TcpSocket socket)
  close(socket)

struct TcpConnectionArgs[T]
  T args
  void!(TcpSocket, T) handler

fn tcpListenStart(int handle, TcpConnectionArgs[T] args)
  TcpSocket socket = { handle = ptrCast(ptr(handle)), alreadyClosed = false }
  void! result = args.handler(socket, args.args)
  close(socket)

pub fn listen(str host, int port, T args, void!(mut TcpSocket, T) handler) void!
  ptr[char] cHost = cstr(host)
  void(int, TcpConnectionArgs[T]) start = tcpListenStart
  int result
  include
    size_t size = sizeof($(TcpConnectionArgs[T]));
    $(TcpConnectionArgs[T])* item = malloc(size);
    item->_args = *_args;
    item->_handler = *_handler;

    _result = listenTcp(_cHost, *_port, item, (void*)_start);
    free(item);
  if result < 0
    return uv_err(result)

  return Ok

pub fn connect(str host, int port) TcpSocket!
  ptr[char] cHost = cstr(host)
  int result
  ptr[void] outHandle

  include
    _result = connectTcp(_cHost, *_port, (TcpHandle*)&_outHandle);
  if result < 0
    return uv_err(result)
  
  return { handle = outHandle, alreadyClosed = false }

pub fn read(mut TcpSocket socket, arr[byte] buf) int!
  int result
  include
    _result = readTcp((void*)_socket->_handle, _buf->_base, _buf->_len);
  if result < 0
    return uv_err(result)
  return result

pub fn write(mut TcpSocket socket, arr[byte] bytes) void!
  int result
  include
    _result = writeTcp((void*)_socket->_handle, _bytes->_base, _bytes->_len);
  if result < 0
    return uv_err(result)
  return Ok

pub fn close(mut TcpSocket socket)
  if socket.alreadyClosed == true
    return
    
  socket.alreadyClosed = true
  include
    closeTcp((TcpHandle)_socket->_handle);
